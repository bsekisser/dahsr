DECLARE SUB CreateCSSFile ()
DECLARE SUB BlankLowerVew (i$)
DECLARE SUB Td (n%, a$, c$, s$, v$, i$)
DECLARE SUB Table (n%, b$, p$, s$, w$)
DECLARE SUB CreateCSS ()
DECLARE SUB CreateGenealogy ()
DECLARE SUB ValidateFile (k$)
DECLARE SUB Purge ()
DECLARE SUB Header (c$)
DECLARE SUB Process (k$)
DECLARE SUB Bufferize ()
DECLARE SUB Scroll (s%, e%, p%)
DECLARE SUB Cuckoo ()
DECLARE SUB Initialize ()
DECLARE SUB Getkey (a%, k$)
DECLARE SUB Main ()
DECLARE SUB Mask ()
DECLARE SUB SayIt (i$, v%, f%, b%)
DECLARE SUB CreateDocument ()
DECLARE SUB AccessLinks (i$, j$)
DECLARE SUB Footer (c$)
DECLARE SUB CreateLinks ()
DECLARE SUB GetFamilyRecord (re$, ge$)
DECLARE SUB GetChildren ()
DECLARE SUB GetNotes ()
	CONST MXL = 76, PRGM = "genelogy.bas", WSZ = 11
	COMMON SHARED Bfr$
	COMMON SHARED Bgc%
	COMMON SHARED Dbse$
	COMMON SHARED Esc$
	COMMON SHARED Ext$
	COMMON SHARED Family$
	COMMON SHARED Fgc%
	COMMON SHARED Filename$
	COMMON SHARED Flt%
	COMMON SHARED Gen$
	COMMON SHARED link$
	COMMON SHARED Mxr$
	COMMON SHARED Nog$
	COMMON SHARED Scr$
	COMMON SHARED Title$
	DIM SHARED Month$(12)
	ON ERROR GOTO Trap
	Initialize
	Bufferize
	Main
END
Trap: Flt% = ERR: RESUME NEXT
DATA "January", "February", "March", "April", "May", "June"
DATA "July","August","September","October","November","December"

'Channel Input and Output Assignments:
'#1: Main Menu Buffer and DataFile Input
'#2: Document.htm Output
'#3: D#.htm Output
'#4: Register.htm Output
'#5: R#,htm Output
'#6: Create Link Output

SUB AccessLinks (i$, j$)
	DIM c$, p$, r$
	q$ = CHR$(34)
	CLOSE #6: OPEN link$ FOR INPUT AS #6
	DO WHILE NOT EOF(6)
	INPUT #6, c$, p$, r$
	IF UCASE$(i$) = c$ AND UCASE$(j$) = p$ THEN
		j$ = "<a href=" + q$ + "r" + r$ + ".htm" + q$ + ">" + p$ + "</a>"
		EXIT DO
	END IF
	LOOP
	CLOSE #6
END SUB

SUB BlankLowerVew (i$)
	FOR x% = WSZ + 7 TO 22
	SayIt i$, x%, 11, Bgc%
	NEXT
END SUB

SUB Bufferize
	DIM i$, j$, x%
	KILL "shell.txt"
	Flt% = 0
	SHELL "dir " + LEFT$(Dbse$, LEN(Dbse$) - 1) + " > shell.txt /B"
	CLOSE : OPEN "shell.txt" FOR INPUT AS #1
	IF Flt% = 0 THEN
		OPEN Bfr$ FOR OUTPUT AS #2
		WHILE NOT EOF(1)
		LINE INPUT #1, i$
		CLOSE #3: OPEN Dbse$ + i$ FOR INPUT AS #3
		IF Flt% = 0 THEN
			DO WHILE NOT EOF(3)
			LINE INPUT #3, j$
			FOR x% = 1 TO LEN(j$)
			SELECT CASE UCASE$(LEFT$(j$, x%))
				CASE "[F]", "[FAMILY]"
					j$ = MID$(j$, x% + 1)
					IF LEFT$(j$, 1) = "," THEN j$ = MID$(j$, 2)
					PRINT #2, LEFT$(i$ + STRING$(MXL, 32), MXL - LEN(j$)); j$
					EXIT DO
			END SELECT
			NEXT
			LOOP
		END IF
		WEND
	END IF
	CLOSE
END SUB

SUB CreateCSSFile
	OPEN Filename$ + "\" + "genelogy.css" FOR OUTPUT AS #2
	PRINT #2, "A:link {text-decoration:none; font-style:normal; font-weight:bold; color:#0000ff;}"
	PRINT #2, "A:visited {text-decoration:none; font-style:normal; font-weight:bold; color:#0000ff;}"
	PRINT #2, "A:active {text-decoration:none;}"
	PRINT #2, "A:hover {text-decoration:none; background-color:#ff0000; font-weight:bold; color:#ffffff;}"
	PRINT #2, ""
	PRINT #2, ".mhdr, .shdr, .mtxt, .stxt, .text, .ital, .path, .desc, .spse, .chld, .note, .rgst, .dsgn, .divs"
	PRINT #2, "    {font-family:georgia, times new roman, ariel;}"
	PRINT #2, ""
	PRINT #2, ".mhdr   {color:#ffffff; background:#0000ff; font-style:normal; font-weight:bold; font-size:30;}"
	PRINT #2, ".shdr   {color:#0000ff; font-style:normal; font-weight:normal; font-size:14;}"
	PRINT #2, ".mtxt   {color:#0000ff; font-style:normal; font-weight:bold; font-size:20;}"
	PRINT #2, ".stxt   {color:#0000ff; font-style:normal; font-weight:bold; font-size:18;}"
	PRINT #2, ".text   {color:#000000; font-style:normal; font-weight:normal; font-size:16;}"
	PRINT #2, ".ital   {color:#000000; font-style:italic; font-weight:normal; font-size:16;}"
	PRINT #2, ".path   {color:#000000; font-style:normal; font-weight:bold; font-size:26;}"
	PRINT #2, ".desc   {color:#000000; font-style:normal; font-weight:bold; font-size:16;}"
	PRINT #2, ".spse   {color:#000000; font-style:normal; font-weight:bold; font-size:16;}"
	PRINT #2, ".chld   {color:#000000; font-style:normal; font-weight:bold; font-size:16;}"
	PRINT #2, ".note   {color:#000000; font-style:normal; font-weight:bold; font-size:16;}"
	PRINT #2, ".rgst   {color:#000000; font-style:normal; font-weight:bold; font-size:16;}"
	PRINT #2, ".dsgn   {color:#000000; font-style:italic; font-weight:bold; font-size:18;}"
	PRINT #2, ".divs   {color:#ffffff; background:#009900; font-style:normal; font-weight:bold; font-size:18;}"
	PRINT #2, ""
	PRINT #2, "BODY    {background:#aaaaff;}"
	PRINT #2, "TABLE   {background:#eeeeff;}"
	CLOSE
END SUB

SUB CreateDocument
	IF Filename$ = "" OR Family$ = "" OR Ext$ = "" THEN EXIT SUB
	DIM d$, o$, q$, t$, x%
	o$ = "+"
	q$ = CHR$(34)
	Flt% = 0
	CLOSE
	OPEN Dbse$ + Filename$ + Ext$ FOR INPUT AS #1
	IF Flt% = 0 THEN
		SayIt "Generating the " + Family$ + " Family Documents", 2, 11, Bgc%
		BlankLowerVew "Generating the " + Family$ + " Family Documents"
		OPEN Filename$ + "\document.htm" FOR OUTPUT AS #2
		Header "2"
		Td 2, "", "shdr", "", "", ""
		PRINT #2, "<a href="; q$; "home.htm"; q$; ">Home</a>&nbsp;-"
		PRINT #2, "&nbsp;<a href="; q$; "register.htm"; q$; ">Register</a>"
		PRINT #2, "</td>"
		PRINT #2, "<tr>"
		Td 2, "", "mhdr", "", "", Family$ + " Family Documents"
		PRINT #2, "</tr>"
		Td 2, "left", "mtxt", "", "", ""
		WHILE NOT EOF(1)
		LINE INPUT #1, i$
		FOR x% = 1 TO LEN(i$)
		SELECT CASE UCASE$(LEFT$(i$, x%))
			CASE "[EOD]"
				o$ = "-"
				PRINT #3, "</td></tr>"
				Footer "3"
				CLOSE #3
				EXIT FOR
			CASE "[DOC]"
				t$ = MID$(i$, x% + 1)
				o$ = "-"
				IF LCASE$(LEFT$(t$, 4)) = "home" THEN
					CLOSE #3: OPEN Filename$ + "\home.htm" FOR OUTPUT AS #3
					Header "3"
					PRINT #2, "<a href="; q$; "home.htm"; q$; ">"; t$; "</a><br>"
				ELSE
					d$ = MID$(STR$(VAL(d$) + 1), 2)
					CLOSE #3: OPEN Filename$ + "\D" + d$ + ".htm" FOR OUTPUT AS #3
					Header "3"
					PRINT #2, "<a href="; q$; "D"; d$; ".htm"; q$; ">"; t$; "</a><br>"
				END IF
		END SELECT
		NEXT
		IF o$ = "+" THEN PRINT #3, i$ ELSE o$ = "+"
		WEND
		PRINT #2, "</td>"
		Footer "2"
	END IF
	CLOSE
END SUB

SUB CreateGenealogy
	IF Filename$ = "" OR Family$ = "" OR Ext$ = "" THEN EXIT SUB
	CreateLinks
	DIM g$, i$, r$, x%
	r$ = "0"
	Flt% = 0
	CLOSE
	OPEN Dbse$ + Filename$ + Ext$ FOR INPUT AS #1
	IF Flt% = 0 THEN
		SayIt "Generating the " + Family$ + " Family Genealogy Register & Records", 2, 11, Bgc%
		BlankLowerVew "Generating the " + Family$ + " Family Genealogy Register & Records"
		OPEN Filename$ + "\register.htm" FOR OUTPUT AS #4
		Header "4"
		Td 4, "", "shdr", "", "", ""
		PRINT #4, "<a href="; q$; "home.htm"; q$; ">Home</a>&nbsp;-"
		PRINT #4, "&nbsp;<a href="; q$; "document.htm"; q$; ">Documents</a>"
		PRINT #4, "</td>"
		PRINT #4, ""
		PRINT #4, "<tr>"
		Td 4, "", "mhdr", "", "", Family$ + " Genealogy Family Register"
		PRINT #4, "</tr>"
		PRINT #4, ""
		PRINT #4, "<tr>"
		Td 4, "", "ital", "", "", ""
		PRINT #4, "Generation:<br>"
		FOR x% = 1 TO VAL(Nog$)
		PRINT #4, "<a href="; q$; "#gen"; MID$(STR$(x%), 2); q$; ">"; MID$(STR$(x%), 2); "</a>";
		IF x% < VAL(Nog$) THEN
			PRINT #4, "&nbsp;";
		END IF
		NEXT
		PRINT #4, "</td></tr>"
		PRINT #4, "<tr>";
		Td 4, "left", "rgst", "", "top", ""
		WHILE NOT EOF(1)
		INPUT #1, i$
		SELECT CASE UCASE$(LEFT$(i$, 3))
			CASE "[D]"
				r$ = MID$(STR$(VAL(r$) + 1), 2)
				GetFamilyRecord r$, g$
			CASE "[G]"
				IF LEN(i$) > 3 THEN
					g$ = STR$(VAL(MID$(i$, 4)))
				ELSE
					g$ = MID$(STR$(VAL(g$) + 1), 2)
				END IF
				PRINT #4, "<div align="; q$; "center"; q$;
				PRINT #4, " class="; q$; "divs"; q$; ">";
				PRINT #4, "<a name="; q$; "gen"; g$; q$; ">Generation: "; g$; "</a></div>"
		END SELECT
		WEND
		PRINT #4, "</td></tr>"
		Footer "4"
	END IF
	CLOSE
END SUB

SUB CreateLinks
	DIM i$, j$, r$
	Mxr$ = "0"
	Nog$ = "0"
	Flt% = 0
	CLOSE
	OPEN Dbse$ + Filename$ + Ext$ FOR INPUT AS #1
	IF Flt% = 0 THEN
		OPEN link$ FOR OUTPUT AS #2
		WHILE NOT EOF(1)
		INPUT #1, i$
		SELECT CASE UCASE$(LEFT$(i$, 3))
			CASE "[C]"
				INPUT #1, j$
				PRINT #2, "[C],"; UCASE$(j$); ","; Mxr$
			CASE "[D]"
				Mxr$ = MID$(STR$(VAL(Mxr$) + 1), 2)
				INPUT #1, j$
				PRINT #2, "[D],"; UCASE$(j$); ","; Mxr$
			CASE "[G]": Nog$ = MID$(STR$(VAL(Nog$) + 1), 2)
		END SELECT
		WEND
	END IF
	CLOSE
END SUB

SUB Cuckoo
	SOUND 1400, 3
	SOUND 0, 2
	SOUND 1200, 4
	SOUND 0, 2
END SUB

SUB Footer (c$)
	DIM n%, x%
	FOR x% = 1 TO LEN(c$)
	n% = VAL(MID$(c$, x%, 1))
	PRINT #n%, "<tr>"
	Td n%, "", "shdr", "", "", MID$(DATE$, 4, 2) + " " + Month$(VAL(LEFT$(DATE$, 2))) + " " + RIGHT$(DATE$, 4)
	PRINT #n%, "</tr>"
	PRINT #n%, "</table>"
	PRINT #n%, "</td>"
	PRINT #n%, "</table>"
	PRINT #n%, "</body>"
	PRINT #n%, "</html>"
	NEXT
END SUB

SUB GetChildren
	DIM p$, c$, b$, d$
	PRINT #5, "<tr>"
	Td 5, "left", "chld", "100", "", ""
	PRINT #5, "<div align="; q$; "center"; q$; " class="; q$; "stxt"; q$; ">";
	PRINT #5, "<u>Children</u></div>"
	INPUT #1, p$, c$, b$, d$
	AccessLinks "[D]", p$
	PRINT #5, p$; ": "; c$;
	IF LEN(b$) > 2 THEN PRINT #5, ", "; b$;
	IF LEN(d$) > 2 THEN PRINT #5, ", "; d$;
	PRINT #5, "<br>"
	DO
	INPUT #1, i$
	SELECT CASE UCASE$(LEFT$(i$, 3))
		CASE "[C]"
			INPUT #1, p$, c$, b$, d$
			AccessLinks "[D]", p$
			PRINT #5, p$; ": "; c$;
			IF LEN(b$) > 2 THEN PRINT #5, ", "; b$;
			IF LEN(d$) > 2 THEN PRINT #5, ", "; d$;
			PRINT #5, "<br>"
		CASE "[N]": GetNotes: EXIT DO
		CASE "[E]": EXIT DO
	END SELECT
	LOOP
	PRINT #5, "</td></tr>"
	PRINT #5, ""
END SUB

SUB GetFamilyRecord (re$, ge$)
	DIM i$, q$
	DIM ph$, de$, db$, dd$, ma$
	DIM sp$, sb$, sd$, pa$
	q$ = CHR$(34)
	CLOSE #5: OPEN Filename$ + "\R" + re$ + ".htm" FOR OUTPUT AS #5
	Header "5"
	Td 5, "", "shdr", "", "", ""
	PRINT #5, "<a href="; q$; "register.htm"; q$; ">Register</a>&nbsp;-";
	PRINT #5, "&nbsp;<a href="; q$; "document.htm"; q$; ">Documents</a>"
	PRINT #5, "</td>"
	PRINT #5, ""
	PRINT #5, "<tr>"
	Td 5, "", "mhdr", "", "", Family$ + " Genealogy Family Record"
	PRINT #5, "</tr>"
	PRINT #5, ""
	INPUT #1, ph$, de$, db$, dd$, ma$
	INPUT #1, sp$, sp$, sb$, sd$, pa$
	i$ = "<a href=" + q$ + "R" + re$ + ".htm" + q$ + ">" + ph$ + "</a>: " + de$
	IF LEN(sp$) > 0 THEN i$ = i$ + " &amp; " + sp$
	i$ = i$ + ".<br>"
	PRINT #4, i$
	AccessLinks "[C]", ph$
	PRINT #5, "<tr>"
	Td 5, "", "stxt", "30", "", "Generation: " + ge$
	Td 5, "", "path", "40", "", ph$
	Td 5, "", "stxt", "30", "", "Record #" + re$
	PRINT #5, "</tr>"
	PRINT #5, ""
	Td 5, "left", "desc", "50", "top", ""
	PRINT #5, "<div align="; q$; "center"; q$; " class="; q$; "mtxt"; q$; "><u>Descendant</u></div>"
	PRINT #5, de$; "<br>";
	IF LEN(db$) > 2 THEN PRINT #5, db$; "<br>"
	IF LEN(dd$) > 2 THEN PRINT #5, dd$; "<br>"
	IF LEN(ma$) > 2 THEN PRINT #5, ma$
	PRINT #5, "</td>"
	Td 5, "left", "spse", "50", "top", ""
	PRINT #5, "<div align="; q$; "center"; q$; " class="; q$; "mtxt"; q$; "><u>Spouse</u></div>"
	PRINT #5, sp$; "<br>";
	IF LEN(sb$) > 2 THEN PRINT #5, sb$; "<br>"
	IF LEN(sd$) > 2 THEN PRINT #5, sd$; "<br>"
	IF LEN(pa$) > 2 THEN PRINT #5, pa$
	PRINT #5, "</td>"
	PRINT #5, "</tr>"
	PRINT #5, ""
	DO WHILE NOT EOF(1)
	INPUT #1, i$
	SELECT CASE UCASE$(LEFT$(i$, 3))
		CASE "[C]": GetChildren: EXIT DO
		CASE "[N]": GetNotes: EXIT DO
		CASE "[U]"
			PRINT #5, "<tr>"
			Td 5, "", "chld", "", "", "Children Unknown"
			PRINT #5, "</tr>"
			PRINT #5, ""
		CASE "[X]"
			PRINT #5, "<tr>"
			Td 5, "", "chld", "", "", "No Children"
			PRINT #5, "</tr>"
			PRINT #5, ""
		CASE "[E]": EXIT DO
	END SELECT
	LOOP
	Footer "5"
	CLOSE #5
END SUB

SUB Getkey (a%, k$)
	DO: LOOP UNTIL INKEY$ = ""
	DO
	k$ = INKEY$
	LOOP WHILE k$ = ""
	a% = ASC(k$)
	IF a% = 27 THEN END
END SUB

SUB GetNotes
	DIM i$
	PRINT #5, "<tr>"
	Td 5, "left", "note", "100", "", ""
	PRINT #5, "<div align="; q$; "center"; q$; " class="; q$; "stxt"; q$; ">";
	PRINT #5, "<u>Notes</u></div>"
	LINE INPUT #1, i$
	PRINT #5, i$; "<br>"
	DO WHILE NOT EOF(1)
	INPUT #1, i$
	SELECT CASE UCASE$(LEFT$(i$, 3))
		CASE "[E]": EXIT DO
		CASE "[N]"
			LINE INPUT #1, i$
			PRINT #5, i$; "<br>"
	END SELECT
	LOOP
	PRINT #5, "</td></tr>"
	PRINT #5, ""
END SUB

SUB Header (c$)
	DIM q$, n%, x%
	q$ = CHR$(34)
	FOR x% = 1 TO LEN(c$)
	n% = VAL(MID$(c$, x%, 1))
	PRINT #n%, "<html>"
	PRINT #n%, "<head><title>"; Family$; " Genealogy</title>"
	PRINT #n%, "<link href="; q$; "genelogy.css"; q$; " rel="; q$; "stylesheet"; q$; " type="; q$; "text/css"; q$; ">"
	PRINT #n%, "</head>"
	PRINT #n%, "<body>"
	PRINT #n%, ""
	Table n%, "2", "", "", "90"
	PRINT #n%, "<td>"
	PRINT #n%, ""
	Table n%, "", "3", "", ""
	PRINT #n%, ""
	NEXT
END SUB

SUB Initialize
	DIM i$, x%
	Bfr$ = "genelogy.bfr"
	Bgc% = 1
	Dbse$ = "dbse\"
	Esc$ = "ù ESC to End ù"
	Ext$ = ""
	Family$ = ""
	Fgc% = 15
	Filename$ = ""
	Flt% = 0
	Scr$ = "ù Scroll (" + CHR$(27) + CHR$(24) + CHR$(25) + CHR$(26) + ") Selections "
	Title$ = "Family Genealogy Generator"
	FOR x% = 1 TO 12
	READ Month$(x%)
	NEXT
END SUB

SUB Main
	DIM a%, e%, k$, p%, s%
	Mask
	CLOSE : OPEN Bfr$ FOR RANDOM AS #1 LEN = MXL + 2
	e% = LOF(1) / (MXL + 2)
	p% = 0
	s% = 1
	DO
	IF p% < 0 THEN p% = 0: s% = s% - 1
	IF p% > WSZ THEN p% = WSZ: s% = s% + 1
	IF s% < 1 THEN s% = 1
	IF s% > e% THEN s% = e%
	Scroll s%, e%, p%
	DO
	Getkey a%, k$
	SELECT CASE UCASE$(k$)
		CASE CHR$(0) + ";", CHR$(0) + "S", CHR$(9), CHR$(13): Process k$
		CASE CHR$(0) + "H", CHR$(0) + "K": p% = p% - 1: EXIT DO
		CASE CHR$(0) + "P", CHR$(0) + "M": p% = p% + 1: EXIT DO
		CASE CHR$(0) + "G": p% = 0: s% = 1: EXIT DO
		CASE CHR$(0) + "O": p% = 1: s% = 1: EXIT DO
	END SELECT
	LOOP
	LOOP
END SUB

SUB Mask
	DIM x%
	CLS
	COLOR Fgc%, Bgc%
	LOCATE 1, 1, 0
	FOR x% = 1 TO 23
	SELECT CASE x%
		CASE 1: PRINT "É"; STRING$(78, 205); "»"
		CASE 3: PRINT "Ì"; STRING$(78, 205); "¹"
		CASE 5, WSZ + 6: PRINT "Ç"; STRING$(78, 196); "¶"
		CASE 23: PRINT "È"; STRING$(78, 205); "¼"
		CASE ELSE: PRINT "º"; STRING$(78, 32); "º"
	END SELECT
	NEXT
	SayIt Title$, 2, 11, Bgc%
	SayIt LEFT$("Filename" + STRING$(MXL, 46), MXL - 6) + "Family", 4, 11, Bgc%
	SayIt Scr$ + Esc$, 22, Fgc%, Bgc%
END SUB

SUB Process (k$)
	IF Filename$ = "" THEN EXIT SUB
	SELECT CASE UCASE$(k$)
		CASE CHR$(0) + ";": SHELL "notepad.exe " + Filename$ + "\" + "genelogy.css": RUN PRGM
		CASE CHR$(0) + "S": Purge: RUN PRGM
		CASE CHR$(9)
			SHELL "notepad.exe " + Dbse$ + Filename$ + Ext$: RUN PRGM
		CASE CHR$(13)
			MKDIR Filename$
			CreateCSSFile
			link$ = Filename$ + "\register.lnx"
			SELECT CASE UCASE$(Ext$)
				CASE ".DOC": CreateDocument: RUN PRGM
				CASE ".DAT", ".TXT"
					ValidateFile k$
					CLOSE
					IF k$ = CHR$(9) THEN
						SHELL "notepad.exe " + Dbse$ + Filename$ + Ext$
					ELSEIF k$ = CHR$(13) THEN
						CreateGenealogy
					END IF
					RUN PRGM
			END SELECT
	END SELECT
	CLOSE
	RUN PRGM
END SUB

SUB Purge
	DIM a%, f$, i$, k$
	SayIt "Delete the " + CHR$(34) + Filename$ + CHR$(34) + " Directory: Are You Sure (Y)?", 2, 13, Bgc%
	Getkey a%, k$
	IF UCASE$(k$) = "Y" THEN
		f$ = "0"
		SHELL "dir " + Filename$ + " > shell.txt /B"
		Flt% = 0
		CLOSE #2: OPEN "shell.txt" FOR INPUT AS #2
		IF Flt% = 0 THEN
			WHILE NOT EOF(2)
			LINE INPUT #2, i$
			KILL Filename$ + "\" + i$
			f$ = MID$(STR$(VAL(f$) + 1), 2)
			IF Flt% > 0 THEN Flt% = 0
			WEND
		END IF
		CLOSE #2
		SayIt "Files Deleted:" + f$ + ", Press ENTER!", 2, 14, Bgc%
	ELSE
		SayIt "Action Aborted, No Files Deleted, Press ENTER!", 2, 11, Bgc%
	END IF
	DO
	Getkey a%, k$
	LOOP UNTIL a% = 13
END SUB

SUB SayIt (i$, v%, f%, b%)
	DIM l$, r$, w%
	w% = 38
	l$ = LEFT$(i$, LEN(i$) / 2)
	r$ = MID$(i$, LEN(l$) + 1)
	l$ = RIGHT$(STRING$(w%, 32) + l$, w%)
	r$ = LEFT$(r$ + STRING$(w%, 32), w%)
	COLOR f%, b%
	LOCATE v%, 3
	PRINT l$; r$
	COLOR Fgc%, Bgc%
END SUB

SUB Scroll (s%, e%, p%)
	DIM b%, d AS STRING * MXL, f%, i$, j$, x%
	Family$ = ""
	Filename$ = ""
	FOR x% = 0 TO WSZ
	SELECT CASE s% + x% - 1
		CASE 1 TO e%: GET #1, s% + x% - 1, d
		CASE ELSE: d = STRING$(MXL, 32)
	END SELECT
	i$ = RTRIM$(LEFT$(d, 12))
	j$ = LTRIM$(MID$(d, LEN(i$) + 1))
	f% = 10
	b% = Bgc%
	IF p% = x% THEN
		SELECT CASE UCASE$(LEFT$(i$, 1))
			CASE "0" TO "9", "A" TO "Z"
				Filename$ = i$
				Family$ = j$
				FOR y% = LEN(Filename$) TO 1 STEP -1
				IF MID$(Filename$, y%, 1) = "." THEN
					Ext$ = MID$(Filename$, y%)
					Filename$ = LEFT$(Filename$, y% - 1)
					EXIT FOR
				END IF
				NEXT
				SELECT CASE UCASE$(Ext$)
					CASE ".DAT", ".TXT"
						SayIt "ù ENTER: Create the " + CHR$(34) + Family$ + CHR$(34) + " Genealogy ù", WSZ + 7, 14, Bgc%
					CASE ".DOC"
						SayIt "ù ENTER: Create the " + CHR$(34) + Family$ + CHR$(34) + " Documents ù", WSZ + 7, 14, Bgc%
				END SELECT
				SayIt "ù TAB: Edit the " + CHR$(34) + Filename$ + Ext$ + CHR$(34) + " Database ù F1: Edit the " + CHR$(34) + "genelogy.css" + CHR$(34) + " File ù", WSZ + 8, 14, Bgc%
				SayIt "ù DELETE: Purge the " + CHR$(34) + Filename$ + CHR$(34) + " Directory ù", WSZ + 9, 14, Bgc%
				SayIt "ù Note: DELETE Does Not Delete the Associated Database ù", WSZ + 10, 11, Bgc%
			CASE ELSE
				SayIt "ù No Action Will Be Initiated ù", WSZ + 7, 11, Bgc%
				SayIt "ù No File is Selected to Generate a Genealogy ù", WSZ + 8, 11, Bgc%
				SayIt "ù No File is Selected to Edit a Database ù", WSZ + 9, 11, Bgc%
				SayIt "ù No File is Selected to Purge a Directory ù", WSZ + 10, 11, Bgc%
		END SELECT
		f% = 15
		b% = 11
	END IF
	IF x% > 0 THEN SayIt LEFT$(i$ + STRING$(MXL, 46), MXL - LEN(j$)) + j$, x% + 5, f%, b%
	NEXT
END SUB

SUB Table (n%, b$, p$, s$, w$)
	DIM q$
	q$ = CHR$(34)
	IF b$ = "" THEN b$ = "0"
	IF p$ = "" THEN p$ = "5"
	IF s$ = "" THEN s$ = "0"
	IF w$ = "" THEN w$ = "100"
	PRINT #n%, "<table";
	PRINT #n%, " align="; q$; "center"; q$;
	PRINT #n%, " border="; q$; b$; q$;
	PRINT #n%, " cellspacing="; q$; s$; q$;
	PRINT #n%, " cellpadding="; q$; p$; q$;
	PRINT #n%, " width="; q$; w$; "%"; q$; ">"
END SUB

SUB Td (n%, a$, c$, s$, v$, i$)
	DIM q$
	q$ = CHR$(34)
	SELECT CASE a$
		CASE "": a$ = "center"
		CASE "<": a$ = "left"
		CASE "^": a$ = "center"
		CASE ">": a$ = "right"
	END SELECT
	IF s$ = "" OR VAL(s$) < 0 OR VAL(s$) > 100 THEN s$ = "100"
	PRINT #n%, "<td";
	PRINT #n%, " align="; q$; a$; q$;
	IF LEN(c$) > 0 THEN PRINT #n%, " class="; q$; c$; q$;
	PRINT #n%, " colspan="; q$; s$; q$;
	IF LEN(v$) > 0 THEN PRINT #n%, " valign="; q$; v$; q$;
	PRINT #n%, " width="; q$; s$ + "%"; q$; ">"
	IF LEN(i$) > 0 THEN PRINT #n%, i$; "</td>"
END SUB

SUB ValidateFile (k$)
	DIM c%, d%, i$, l%, x%
	l% = 0
	CLOSE : OPEN Dbse$ + Filename$ + Ext$ FOR INPUT AS #1
	DO WHILE NOT EOF(1)
	c% = 0
	d% = 0
	LINE INPUT #1, i$
	l% = l% + 1
	SELECT CASE UCASE$(LEFT$(i$, 3))
		CASE "[D]": d% = 5
		CASE "[C]", "[S]": d% = 4
		CASE "[E]", "[F]", "[G]", "[N]", "[U]", "[X]": d% = 0
		CASE ELSE: d% = 9
	END SELECT
	SELECT CASE d%
		CASE 1 TO 8
			FOR x% = 1 TO LEN(i$)
			IF MID$(i$, x%, 1) = "," THEN c% = c% + 1
			NEXT
			IF c% <> d% THEN
				SayIt "Data Count Error on Line #" + MID$(STR$(l%), 2) + ", Press a key!", 2, 14, Bgc%
				SayIt CHR$(34) + i$ + CHR$(34), WSZ + 7, 14, Bgc%
				Getkey a%, k$
				IF k$ = CHR$(9) THEN EXIT DO
			END IF
		CASE 9
			SayIt CHR$(34) + UCASE$(LEFT$(i$, 3)) + CHR$(34) + " is a Code Error at Line #" + MID$(STR$(l%), 2) + "!", 2, 14, Bgc%
			Getkey a%, k$
			IF k$ = CHR$(9) THEN EXIT DO
	END SELECT
	LOOP
	CLOSE
END SUB

